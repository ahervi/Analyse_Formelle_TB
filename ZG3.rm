-- fichier ZG3.rm
-- protocole de non repudiation de Zhou & Gollmann 97 offline 2
----> perte de message
----> retablissement
----> timers locaux
----> 1 timer global

-- initiateur honnete de la communication : Ah
-- participant honnete a la communication : Bh
-- tiers de confiance : 1/3_C

-- reseau a delai fini mais non fiable : reseau_delai_fini_non_fiable_A_B_Tiers
-- fiables : Ah <--> 1/3_C et Bh <--> 1/3_C ; non fiable : Ah <--> Bh

-- Introduction de timers representes par des atomes : 
------ 3 Timers locaux :
---------- pour A : timerr_ATmsg2 arme après l'emission du premier message 
---------- pour A : timerr_ATmsg4 arme après l'emission du troisième message 
---------- pour B : timerr_B1 arme après l'emission du deuxième message
------ Un timer globale : timerr
-- Un seul module Horloge regroupant les timers



module Ah

	external 

		M2 , M4, M7  : bool;

		TimeOut_ATmsg2, TimeOut_ATmsg4, TimeOut_Tglobal : bool

	interface	

		pcA : (0..8);

		sendM1, sendM3, sendM5 : bool;

		Ma , Ka, Ca : bool; 

		EOOa, EORa, EOOk_a, EORk_a, SUBk_a, CONk_a : bool; 

		f_EOOa, f_EOOk_a, f_EORa, f_EORk_a, f_SUBk_a, f_CONk_a : bool;

		STOPa : bool;

		timer_triggered_ATmsg2, timer_triggered_ATmsg4, timer_triggered_Tglobal : bool
	

	atom A_honest

	controls

		pcA,

		sendM1,	sendM3, sendM5,

		Ma, Ka, Ca,

		EOOa, EORa, EOOk_a, EORk_a, SUBk_a, CONk_a,

		f_EOOa, f_EORa, f_EOOk_a, f_EORk_a, f_SUBk_a, f_CONk_a,

		STOPa,

		timer_triggered_ATmsg2, timer_triggered_ATmsg4, timer_triggered_Tglobal


	reads

		M2, M4, M7,

		TimeOut_ATmsg2, TimeOut_ATmsg4, TimeOut_Tglobal,

		pcA,

		sendM1, sendM3,

		Ma, Ka, Ca,

		EOOa, EORa, EOOk_a, EORk_a, SUBk_a, CONk_a,

		f_EOOa, f_EOOk_a, f_EORa, f_EORk_a, f_SUBk_a, f_CONk_a,

		STOPa,

		timer_triggered_ATmsg2, timer_triggered_ATmsg4, timer_triggered_Tglobal

		

		

		

		

	init

		[] true -> 	

			pcA':=0;

			sendM1':= false; sendM3':= false; sendM5':= false;

			Ma':= true; Ka':= true; Ca':= false;

			EOOa':= false; EORa':= false; EOOk_a':= false; EORk_a':= false; SUBk_a':= false, CONk_a':=false,

			f_EOOa':= true; f_EORa':= false; f_EOOk_a':= true; f_EORk_a':= false; f_SUBk_a':= false, f_CONk_a':=false,

			STOPa':= false;

			timer_triggered_ATmsg2':= false; timer_triggered_ATmsg4':= false; timer_triggered_Tglobal':=false


				



 	update 

-- calcul de Ca = h(Ka,Ma)

	[] (pcA=0) & Ma & Ka & ~Ca -> Ca':=true; pcA':=1 



-- calcul de EOOa

	[] (pcA=1) & f_EOOa & Ca -> EOOa':= true; pcA':=2



-- emission de m1 et armement de Tmsg2

	[] (pcA=2) & f_EOOa & Ca & EOOa -> sendM1':= true; pcA':=3; timer_triggered_ATmsg2':= true; timer_triggered_Tglobal':=true


-- expiration de Tmsg2 apres l'emission de m1

	[] (pcA=3) & sendM1 & ~M2 & TimeOut_ATmsg2 -> pcA':=7


-- reception de m2

	[] (pcA=3) & sendM1 & M2 -> f_EORa':= true; EORa':= true; pcA':=4



-- calcul de EOOk_a

	[] (pcA=4) & f_EOOk_a & Ka -> EOOk_a':= true; pcA':=5



-- emission de m3 et armement de Tmsg4

	[] (pcA=5) & f_EOOk_a & Ka & EOOk_a -> sendM3':= true; timer_triggered_ATmsg4':= true; f_SUBk_a:=true; pcA':=6;


-- expiration de Tmsg4 apres l'emission de m3 SANS retablissement

	[] (pcA=6) & sendM3 & ~M4 & TimeOut_ATmsg4 & TimeOut_Tglobal -> pcA':=7


-- expiration de Tmsg4 : debut du retablissment par le calcul de SUBk_a 

	[] (pcA=6) & sendM3 & ~M4 & TimeOut_ATmsg4 & ~TimeOut_Tglobal & f_SUBk_a -> SUBk_a':=true; pcA':=9


-- suite du retablissment : envoie de m5

	[] (pcA=9) & SUBk_a -> sendM5':=true; pcA':=10


-- suite du retablissment : reception de m7

	[] (pcA=10) & sendM5 & M7 & ~TimeOut_Tglobal -> f_CONk_a':=true; CONk_a':=true; pcA':= 7

-- suite du retablissment : expiration de Tglobal en attendant de m7

	[] (pcA=10) & sendM5 & ~M7 & TimeOut_Tglobal -> pcA':= 7

-- reception de m4

	[] (pcA=6) & M4 -> f_EORk_a':=true; EORk_a':=true; pcA':=7



-- arret de A

	[] (pcA=7) -> STOPa':=true; pcA':=8



endatom

endmodule


module Bh

	external 

		M1 , M3 : bool;

		TimeOut_BTmsg3, TimeOut_Tglobal : bool;


	interface	

		pcB : (0..8);

		sendM2, sendM4 : bool;

		Mb , Kb, Tb, Cb : bool; 

		EOOb, EORb, EOOk_b, EORk_b, CONk_b : bool; 

		f_EOOb, f_EOOk_b, f_EORb, f_EORk_b, f_CONk_b : bool;

		STOPb : bool;
		
		timer_triggered_BTmsg3: bool

	

	atom B_honest

	controls

		pcB,

		sendM2,	sendM4,

		Mb, Kb, Tb, Cb,

		EOOb, EORb, EOOk_b, EORk_b, CONk_b

		f_EOOb, f_EORb, f_EOOk_b, f_EORk_b, f_CONk_b

		STOPb,

		timer_triggered_BTmsg3




	reads

		M1, M3,

		TimeOut_BTmsg3, TimeOut_Tglobal,

		pcB,

		sendM2, sendM4,

		Mb, Kb, Cb, Tb,

		EOOb, EORb, EOOk_b, EORk_b, CONk_b

		f_EOOb, f_EOOk_b, f_EORb, f_EORk_b, f_CONk_b,

		STOPb,

		timer_triggered_BTmsg3

		

		

		

	init

		[] true -> 	

			pcB':=0;

			sendM2':= false; sendM4':= false;

			Mb':= false; Kb':= false; Tb':= false; Cb':= false;

			EOOb':= false; EORb':= false; EOOk_b':= false; EORk_b':= false; CONk_b':= false;

			f_EOOb':= false; f_EORb':= true; f_EOOk_b':= false; f_EORk_b':= true; f_CONk_b':= false;

			STOPb':= false;

			timer_triggered_BTmsg3':=false

				



 	update 

-- reception de m1

	[] (pcB=0) & M1 -> f_EOOb':= true; EOOb':= true; pcB':=1; Cb':= true



-- calcul de EORb

	[] (pcB=1) & f_EOOb & EOOb & Cb -> f_EORb' := true; EORb':= true; pcB':=2



-- emission de m2

	[] (pcB=2) & f_EORb & EORb -> sendM2':= true; pcB':=3; timer_triggered_BTmsg3':= true



-- reception de m3

	[] (pcB=3) & sendM2 & M3 -> f_EOOk_b':= true; EOOk_b':= true; Kb' := true; pcB':=4


-- expiration de Tmsg3 : SANS retablissment

	[] (pcB=3) & sendM2 & ~M3 & TimeOut_BTmsg3 & TimeOut_Tglobal -> pcB':=6


-- expiration de Tmsg3 : debut retablissment

	[] (pcB=3) & sendM2 & ~M3 & TimeOut_BTmsg3 & ~TimeOut_Tglobal -> pcB':=8

-- suite retablissement : reception de m6

	[] (pcB=8) & M6 -> CONk_b':=true; f_CONk_b':=true; pcB':=6

-- suite retablissement : expiration de Tglobal

	[] (pcB=8) & ~M6 & TimeOut_Tglobal -> pcB':=6


-- calcul de EORk_b

	[] (pcB=4) & f_EOOk_b & EOOk_b & Kb -> f_EORk_b' := true; EORk_b':= true; pcB':=5



-- emission de m4

	[] (pcB=5) & f_EORk_b & Kb & EORk_b -> sendM4':= true; pcB':=6



-- arret de B

	[] (pcB=6) -> STOPb':=true; pcB':=7



endatom

endmodule



module Horloge
	external timer_triggered_ATmsg2, timer_triggered_ATmsg4, timer_triggered_BTmsg3, timer_triggered_Tglobal : bool
	interface TimeOut_ATmsg2, TimeOut_ATmsg4, TimeOut_BTmsg3, TimeOut_Tglobal : bool
	private timerOn_ATmsg2, timerOn_ATmsg4, timerOn_BTmsg3, timerOn_Tglobal : bool
	
	atom timerr_ATmsg2
	controls TimeOut_ATmsg2, timerOn_ATmsg2
	reads timer_triggered_ATmsg2, timerOn_ATmsg2, TimeOut_ATmsg2
	
	init	[] true -> TimeOut_ATmsg2':= false; timerOn_ATmsg2':=false
	
	update
		[] timer_triggered_ATmsg2 & ~TimeOut_ATmsg2 -> timerOn_ATmsg2':=true
		[] timerOn_ATmsg2 -> 
		[] timerOn_ATmsg2 -> TimeOut_ATmsg2':=true; timerOn_ATmsg2':=false
	endatom

	
	atom timerr_ATmsg4
	controls TimeOut_ATmsg4, timerOn_ATmsg4
	reads timer_triggered_ATmsg4, timerOn_ATmsg4, TimeOut_ATmsg4
	
	init	[] true -> TimeOut_ATmsg4':= false; timerOn_ATmsg4':=false
	
	update
		[] timer_triggered_ATmsg4 & ~TimeOut_ATmsg4 -> timerOn_ATmsg4':=true
		[] timerOn_ATmsg4 -> 
		[] timerOn_ATmsg4 -> TimeOut_ATmsg4':=true; timerOn_ATmsg4':=false
	endatom

	
	atom timerr_BTmsg3
	controls TimeOut_BTmsg3, timerOn_BTmsg3
	reads timer_triggered_BTmsg3, timerOn_BTmsg3, TimeOut_BTmsg3
	
	init	[] true -> TimeOut_BTmsg3':= false; timerOn_BTmsg3':=false
	
	update
		[] timer_triggered_BTmsg3 & ~TimeOut_BTmsg3 -> timerOn_BTmsg3':=true
		[] timerOn_BTmsg3 -> 
		[] timerOn_BTmsg3 -> TimeOut_BTmsg3':=true; timerOn_BTmsg3':=false
	endatom

	atom timerr_Tglobal
	controls TimeOut_Tglobal, timerOn_Tglobal
	reads timer_triggered_Tglobal, timerOn_Tglobal, TimeOut_Tglobal
	
	init	[] true -> TimeOut_Tglobal':= false; timerOn_Tglobal':=false
	
	update
		[] timer_triggered_Tglobal & ~TimeOut_Tglobal -> timerOn_Tglobal':=true
		[] timerOn_Tglobal -> 
		[] timerOn_Tglobal -> TimeOut_Tglobal':=true; timerOn_Tglobal':=false
	endatom

endmodule


module Tiers_de_confiance 
	external 
		M5 : bool;
		TimeOut_Tglobal : bool
	interface
		sendM6, sendM7 : bool;
		L, T, K : bool;
		SUBk, CONk : bool;
		f_SUBk, f_CONk : bool
	atom Tiers_de_confiance_deterministe
	controls
		sendM6, sendM7,
		L, T, K,
		SUBk, CONk,
		f_SUBk, f_CONk 
	reads
		M5, sendM6, sendM7,
		L, T, K,
		CONk,
		f_SUBk, f_CONk,
		TimeOut_Tglobal
	init
		[] true ->
			sendM6':=false; sendM7':=false;
			L':=false; T':=false; K':=false;
			f_SUBk':= false; f_CONk':=false;
			SUBk':=false; CONk':=false
	update
-- demande de retablissement par la reception de m5
	[] M5 & ~f_SUBk & ~TimeOut_Tglobal -> f_SUBk':=true; SUBk':=true; L':=true; T':=true; K':=true
-- calcul de CONk
	[] f_SUBk & ~CONk & L & T & K & ~f_CONk -> CONk':=true; f_CONk':=true
-- retablissement des preuves partielles par l'emission de m6 et m7 
	[] f_CONk & CONk & L & T & K & ~sendM6 & ~sendM7 -> sendM6':=true; sendM7':=true
endatom
endmodule

module reseau_delai_fini_non_fiable_A_B_Tiers
external sendM1, sendM3, sendM2, sendM4, sendM5, sendM6, sendM7 : bool
interface M1, M3, M2, M4, M5, M6, M7 : bool

	atom canal_A2B_non_fiable
	controls M1, M3
	reads sendM1, sendM3, M1, M3
	init	[] true -> M1':=false; M3':=false
	update
-- transmission fiable de M1		
	[] sendM1 & ~M1 -> M1':=true
-- transmission non fiable de M1	
	[] sendM1 & ~M1 -> M1':=M1
-- transmission fiable de M3
	[] sendM3 & ~M3 -> M3':=true
-- transmission non fiable de M3
	[] sendM3 & ~M3 -> M3':=M3
	endatom

	atom canal_A2Tiers_fiable
	controls M5
	reads sendM5, M5
	init [] true -> M5':=false
	update
-- transmission de M5
	[] sendM5 & ~M5 -> M5':=true
endatom

	atom canal_B2A_non_fiable
	controls M2, M4
	reads sendM2, sendM4, M2, M4
	init 	[] true -> M2':=false; M4':=false
	update
-- transmission fiable de M2
	[] sendM2 & ~M2 -> M2':=true
-- transmission non fiable de M2
	[] sendM2 & ~M2 -> M2':=M2
-- transmission fiable de M4
	[] sendM4 & ~M4 -> M4':=true
-- transmission non fiable de M4
	[] sendM4 & ~M4 -> M4':=M4
endatom

atom canal_Tiers2AetB_fiable
	controls M6, M7
	reads sendM6, sendM7, M6, M7
	init [] true -> M6':=false; M7':=false
	update
-- transmission de M6
	[] sendM6 & ~M6 -> M6':=true
-- transmission de M7
	[] sendM7 & ~M7 -> M7':=true
endatom
endmodule

ZG3 := Ah || Bh || Tiers_de_confiance || reseau_delai_fini_non_fiable_A_B_Tiers || Horloge
